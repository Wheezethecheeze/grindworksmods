extends Quest
class_name QuestCog

const OBJECTIVE_RANGE := Vector2i(10,15)
const FALLBACK_ICON := preload("res://ui_assets/quests/gear2.png")

@export var specific_cog : CogDNA
@export var department := CogDNA.CogDept.NULL
@export_range(1,12) var min_level := 1
var prev_quest_roll := -1


func _init() -> void:
	BattleService.s_battle_participant_died.connect(participant_died)

func setup() -> void:
	# Reset the quest
	reset()
	
	# Get item
	super()
	
	randomize_objective()
	
	title = "WANTED"
	quota_text = "defeated"
	
	if quota == 1: quest_txt += "A "
	else: quest_txt += str(quota) + " "
	
	if min_level > 1:
		quest_txt += "Level "+str(min_level)+"+ "
	
	if specific_cog:
		var cog_name: String
		
		if quota > 1: cog_name = specific_cog.get_plural_name()
		else: cog_name = specific_cog.cog_name
		
		if cog_name.begins_with("The "):
			cog_name = cog_name.lstrip("The ")
		quest_txt += cog_name
	elif not department == CogDNA.CogDept.NULL:
		var dept_name := Cog.get_department_name(department) + "bot"
		dept_name[0] = dept_name[0].to_upper()
		quest_txt += dept_name
	else:
		quest_txt += "Cog"
	if (quota > 1 and not quest_txt.ends_with("s")) and not specific_cog:
		quest_txt += "s"
	
	s_quest_updated.emit()

func randomize_objective() -> void:
	quota = RNG.channel(RNG.ChannelQuests).randi_range(OBJECTIVE_RANGE.x, OBJECTIVE_RANGE.y)
	var quotaf := float(quota)
	
	var quest_type = RNG.channel(RNG.ChannelCogQuestTypes).randi() % 3
	if quest_type == 1 and prev_quest_roll == 1:
		quest_type += 1 * RNG.channel(RNG.ChannelCogQuestTypes).pick_random([-1, 1])
	
	var level_ranges := FloorVariant.LEVEL_RANGES
	var floor_num: int = max(Util.floor_number, 0)
	
	var minimum_level: int = mini(0, level_ranges[floor_num][0] - 1)
	var maximum_level: int = mini(7, level_ranges[floor_num][1] - 1)
	
	# 33% chance of department specific
	if quest_type == 0:
		department = goal_dept
	elif quest_type == 1:
		var cog_pool : CogPool
		match goal_dept:
			CogDNA.CogDept.SELL:
				cog_pool = load('res://objects/cog/presets/pools/sellbot.tres')
			CogDNA.CogDept.CASH:
				cog_pool = load('res://objects/cog/presets/pools/cashbot.tres')
			CogDNA.CogDept.LAW:
				cog_pool = load('res://objects/cog/presets/pools/lawbot.tres')
			CogDNA.CogDept.BOSS:
				cog_pool = load('res://objects/cog/presets/pools/bossbot.tres')
				
		specific_cog = cog_pool.cogs[RNG.channel(RNG.ChannelCogQuestTypes).randi_range(minimum_level, maximum_level)]
	
	# Reduce quotas for more specific quest types
	if not department == CogDNA.CogDept.NULL:
		quotaf /= 2.0
	elif specific_cog:
		quotaf /= 4.0
	
	# Level minimum objectives
	if RNG.channel(RNG.ChannelCogQuestTypes).randi() % 3 == 0:
		if specific_cog:
			min_level = RNG.channel(RNG.ChannelCogQuestTypes).randi_range(specific_cog.level_low + 1, specific_cog.level_low + 3)
			if min_level > specific_cog.level_high or min_level > maximum_level: 
				min_level = 1
		else:
			min_level = RNG.channel(RNG.ChannelCogQuestTypes).randi_range(minimum_level, maximum_level)
	
	if min_level > 1:
		quotaf /= maxf(min_level/4.0,1.25)
	
	quota = int(round(quotaf))

func participant_died(participant: Node3D) -> void:
	var cog: Cog
	if not participant is Cog or quota <= current_amount:
		return
	elif participant is Cog:
		cog = participant
		if not BattleService.cog_gives_credit(cog): return
	
	if specific_cog:
		if cog.fusion:
			if not specific_cog.battle_phrases.hash() == cog.dna.battle_phrases.hash():
				return
		else:
			if not specific_cog.cog_name == cog.dna.cog_name:
				return
	
	if not department == CogDNA.CogDept.NULL:
		if not cog.dna.department == department:
			return
	
	if min_level > 1:
		if cog.level < min_level:
			return
	
	# If no check has failed, quota increments
	current_amount += 1
	s_quest_updated.emit()
	
	if current_amount == quota:
		s_quest_complete.emit()

func uses_3d_model() -> bool:
	if specific_cog:
		return true

	return false

func get_3d_model() -> Node3D:
	if not specific_cog:
		return Node3D.new()

	var head: Node3D = specific_cog.get_head()
	head.scale = specific_cog.head_scale
	return head

func get_icon() -> Texture2D:
	if not department == CogDNA.CogDept.NULL:
		return Cog.get_department_emblem(department)
	else:
		return FALLBACK_ICON

func reset() -> void:
	super()
	if specific_cog:
		prev_quest_roll = 1
	elif not department == CogDNA.CogDept.NULL:
		prev_quest_roll = 0
	specific_cog = null
	department = CogDNA.CogDept.NULL
	min_level = 1
